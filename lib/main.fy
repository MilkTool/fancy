require: "boot"
require("rbx/eval")

ARGV for_options: ["-v", "--version"] do: {
  "fancy " ++ FANCY_VERSION println
  "(C) 2010 Christopher Bertels <chris@fancy-lang.org>" println
}

ARGV for_options: ["--help", "-h"] do: {
  ["Usage: fancy [option] [programfile] [arguments]",
   "  --help        Print this output",
   "  -h            Print this output",
   "  --version     Print Fancy's version number",
   "  -v            Print Fancy's version number",
#   "  -I directory  Add directory to Fancy's LOAD_PATH",
   "  -e 'command'  One line of Fancy code that gets evaluated immediately",
   "  -c            Compile given files to Rubinius bytecode",
   "  -cv           Compile given files to Rubinius bytecode verbosely (outputting the generated bytecode)."] println
}

ARGV for_option: "-e" do: |eval_string| {
  eval_string eval
  System exit # quit when running with -e
}

ARGV for_option: "-c" do: {
  ARGV index: "-c" . if_do: |idx| {
    ARGV[[idx + 1, -1]] each: |filename| {
      "Compiling " ++ filename println
      System do: ("rbx rbx/compiler.rb " ++ filename)
    }
  }
  System exit
}

ARGV for_option: "-cv" do: {
  ARGV index: "-cv" . if_do: |idx| {
    ARGV[[idx + 1, -1]] each: |filename| {
      "Compiling " ++ filename println
      System do: ("rbx rbx/compiler.rb " ++ filename ++ " -B")
    }
  }
  System exit
}

ARGV first if_do: |file| {
  Fancy::CodeLoader load_compiled_file(file)
}

