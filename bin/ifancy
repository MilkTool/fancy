#!/usr/bin/env fancy

require("readline")

["Welcome to the (still very simple) Fancy REPL",
 "Fancy " ++ FANCY_VERSION] println

ARGV rest each: |file| {
  "LOADING: " ++ file println
  require: file
}

# handle SIGINT
trap("INT") {
  "Quitting." println
  System exit
}

Console newline;

def double_or_empty?: line {
  (line =~ /^\s*$/) or: (Readline HISTORY to_a[-2] == line)
}

try {
  { Readline readline(">> ", true) } while_do: |line| {

    double_or_empty?: line . if_true: {
      Readline::HISTORY pop()
    }

    line =~ /^\s*$/ if_false: {
      try {
        line eval inspect println
      } catch Exception => e {
        e message println
      }
    }
  }
} catch Interrupt => e {
  System exit
}
